apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar, dependsOn: assemble) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = releaseConfig.group
                version = releaseConfig.versionName

                artifactId = "complexify-core"

                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'Complexify Core'
                    description = 'A library which makes checking the quality of user\'s password a breeze.'
                    url = 'https://github.com/infinum/android-complexify'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'jplesko'
                            name = 'Jonatan Ple≈°ko'
                            email = 'jonatan.plesko@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/android-complexify.git'
                        developerConnection = 'https://github.com/infinum/android-complexify.git'
                        url = 'https://github.com/infinum/android-complexify'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}
